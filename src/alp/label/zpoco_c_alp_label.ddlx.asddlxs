@Metadata.layer: #CORE
@UI.headerInfo: { typeName: 'Label',
                  typeNamePlural: 'Labels' }
                  
@UI.selectionPresentationVariant:[ 
    {  //chart and table
        qualifier: 'Default',
        presentationVariantQualifier: 'Default',
        selectionVariantQualifier: 'Default'
    },
    {   //kpi
        qualifier: 'KPITherapyPrintedLabels',
        presentationVariantQualifier: 'pvKPITherapyPrintedLabels',
        selectionVariantQualifier: 'svKPITherapyPrintedLabels'
    }    
]


@UI.presentationVariant:[
    { //main area chart
        qualifier: 'Default',
        visualizations: [{
            type: #AS_CHART,
            qualifier: 'ChartDefault'
        }]
    },
    
    {   //visual filter TherapyTypeId
        qualifier: 'pvFilterByTherapyTypeID',
        text: 'Filter: Therapy Type ID',
        sortOrder: [{ by: 'TherapyTypeId' }] ,
        visualizations: [{
            type: #AS_CHART,
            qualifier: 'ChartFilterByTherapyTypeID'
        }]
    },
    
    
    {   //kpi
        qualifier: 'pvKPITherapyPrintedLabels',
        text: 'KPI Printed Labels by Therapy',
        visualizations: [{
            type: #AS_CHART,
            qualifier: 'cqKPITherapyPrintedLabels'
        },{
            type: #AS_DATAPOINT,
            qualifier: 'PrintedLabels'
        }]
    }    
    
    ]

@UI.selectionVariant:[
    { //main area
        qualifier: 'Default',
        text: 'Default'
    },
    
    { //kpi
        qualifier: 'svKPITherapyPrintedLabels',
        text: 'KPI Printed LAbels by Therapy'
    }
]


@UI.chart: [
//         Charts
      { //visual TherapyTypeID
        qualifier: 'ChartFilterByTherapyTypeID',
        chartType: #BAR,
        dimensions:  [ 'TherapyTypeId' ],
        measures:  [ 'PrintedLabels' ],
        dimensionAttributes: [{
            dimension: 'TherapyTypeId',
            role: #CATEGORY
        }],
        measureAttributes: [{
            measure: 'PrintedLabels',
            role: #AXIS_1
        }]
      },

      { //kpi Therapy
        qualifier: 'cqKPITherapyPrintedLabels',
        chartType: #COLUMN,
        dimensions:  [ 'Therapy' ],
        measures:  [ 'PrintedLabels' ],
        dimensionAttributes: [{
            dimension: 'Therapy',
            role: #CATEGORY
        }],
        measureAttributes: [{
            measure: 'PrintedLabels',
            role: #AXIS_1
        }]
      },

      {
        qualifier: 'ChartDefault',
        chartType: #COLUMN_DUAL,
        dimensions:  [ 'MfgPlantId' ],
        measures:  [ 'PrintedLabels', 'UsedLabels' ],
        dimensionAttributes: [{
            dimension: 'MfgPlantId',
            role: #SERIES
        }],
        measureAttributes: [{
            measure: 'PrintedLabels' ,
            role: #AXIS_1 },
        {   measure:  'UsedLabels',
            role: #AXIS_2 }]
      }
  ]
    
    
annotate view zpoco_c_alp_label with
{
  @UI.facet: [{
                  id:              'GeneralInformation',
                  type:            #COLLECTION,
                  label:           'General Information',
                  position:        10
               }, 
              { 
                  id:              'AdminDataCollection',
                  type:            #COLLECTION,
                  label:           'Admin Data',
                  position:        20
               }, 
              { 
                  id:              'BasicData',
                  purpose:         #STANDARD ,
                  parentId:        'GeneralInformation',
                  type:            #FIELDGROUP_REFERENCE,
                  label:           'Basic Data',
                  position:        10,
                  targetQualifier: 'BasicData'
               }, 
              { 
                  id:              'AdminData',
                  purpose:         #STANDARD ,
                  parentId:        'AdminDataCollection',
                  type:            #FIELDGROUP_REFERENCE,
                  label:           'Admin Data',
                  position:        20,
                  targetQualifier: 'AdminData'
               }]

  @UI.hidden: true
  LabelUuid;    
  
  @UI.lineItem:       [{ position: 10, importance: #HIGH }]
  @UI.fieldGroup:     [{ position: 10, qualifier: 'BasicData' }]
  Therapy;   
  
  @UI.selectionField: [{ position: 20 }]
  @UI.lineItem:       [{ position: 20, importance: #HIGH }]
  @UI.fieldGroup:     [{ position: 20, qualifier: 'BasicData' }]  
  MfgPlantId ;  
  
  @UI.selectionField: [{ position: 30 }]
  @UI.lineItem:       [{ position: 30, importance: #HIGH }]
  @UI.fieldGroup:     [{ position: 30, qualifier: 'BasicData' }]  
  TherapyTypeId  ;    
  
//  @UI.selectionField: [{ position: 40 }]
  @UI.lineItem:       [{ position: 40, importance: #HIGH }]
  @UI.fieldGroup:     [{ position: 40, qualifier: 'BasicData' }]
  NvsId  ;      
  
  @UI.lineItem:       [{ position: 50, importance: #HIGH }]
  @UI.fieldGroup:     [{ position: 50, qualifier: 'BasicData' }]
  Reconcile    ;

  @UI.lineItem:       [{ position: 60, importance: #HIGH }]
  @UI.fieldGroup:     [{ position: 60, qualifier: 'BasicData' }]
  @UI.dataPoint: {
       qualifier: 'PrintedLabels',
       title: 'Printed Labels',
       criticalityCalculation: {
           improvementDirection: #MAXIMIZE,
           toleranceRangeLowValue:  30,
           toleranceRangeHighValue: 100
        }
  }  
  PrintedLabels;

  @UI.lineItem:       [{ position: 70, importance: #HIGH }]
  @UI.fieldGroup:     [{ position: 70, qualifier: 'BasicData' }]
  UsedLabels   ;

  @UI.fieldGroup:     [{ position: 10, qualifier: 'AdminData' }] 
  CreatedBy    ;
 
  @UI.fieldGroup:     [{ position: 20, qualifier: 'AdminData' }]  
  CreatedAt    ;
 
  @UI.fieldGroup:     [{ position: 30, qualifier: 'AdminData' }]  
  LastChangedBy;
  
  @UI.fieldGroup:     [{ position: 40, qualifier: 'AdminData' }] 
  LastChangedAt ;

}