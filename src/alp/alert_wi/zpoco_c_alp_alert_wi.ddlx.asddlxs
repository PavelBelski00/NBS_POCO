@Metadata.layer: #CORE

@UI.headerInfo: { typeName:       'Work Item Alert',
                  typeNamePlural: 'Work Item Alerts' }

@UI.selectionPresentationVariant: [
                                   {   //kpi
                                       qualifier: 'KPIOrdersQuantityWI',
                                       presentationVariantQualifier: 'pvKPIOrdersQuantityWI',
                                       selectionVariantQualifier: 'svKPIOrdersQuantityWI'
                                   },
                                   {  //chart and table
                                       qualifier: 'Default',
                                       presentationVariantQualifier: 'Default',
                                       selectionVariantQualifier: 'Default'
                                   }
                                  ]

@UI.presentationVariant: [
                          { //kpi
                            qualifier:     'pvKPIOrdersQuantityWI',
                            text:          'KPI: Orders Quantity',
                            visualizations: [{
                                               type: #AS_CHART,
                                               qualifier: 'cqKPIOrdersQuantity'
                                             },
                                             {
                                               type: #AS_DATAPOINT,
                                               qualifier: 'OrdersQuantity'
                                            }]
                          },
                          { //visual filter
                            qualifier:     'pvFilterByAlertMessages',
                            text:          'Filter: Alert Messages',
                            sortOrder:      [{ by: 'AlertWIMessage', direction: #ASC }] ,
                            visualizations: [{ 
                                               type: #AS_CHART, 
                                               qualifier: 'ChartFilterAlertMessages' 
                                            }]
                          },
                          { //visual filter
                            qualifier:     'pvFilterByAlertMonth',
                            text:          'Filter: Alert Month',
                            sortOrder:      [{ by: 'AlertWIMonth', direction: #ASC }] ,
                            visualizations: [{ 
                                               type: #AS_CHART, 
                                               qualifier: 'ChartFilterAlertMonth' 
                                            }]
                          },
                          { //visual filter
                            qualifier:     'pvAlertQuarter',
                            text:          'Filter:  Alert Quarter',
                            sortOrder:      [{ by: 'AlertWIQuarter', direction: #DESC }] ,
                            visualizations: [{                 
                                               type:       #AS_CHART,                 
                                               qualifier: 'ChartFilterAlertQuarter'                 
                                            }]                 
                          },                 
                          
                          { //main area chart                 
                            qualifier:     'Default',                 
                            sortOrder:      [{ by: 'UpdatedDate' , direction: #ASC }] ,                 
                            visualizations: [{                 
                                               type: #AS_CHART,                 
                                               qualifier: 'ChartDefault'                 
                                            }]                 
                          }                 
                         ] 

@UI.chart: [
            { //kpi
              qualifier:           'cqKPIOrdersQuantity',
              chartType:           #COLUMN,
              dimensions:          [ 'AlertWIMessage' ],
              measures:            [ 'OrdersQuantity' ],
              dimensionAttributes: [{ dimension: 'AlertWIMessage', role: #CATEGORY }],
              measureAttributes:   [{ measure: 'OrdersQuantity', role: #AXIS_1 }]
            },
            { //visual filter
              qualifier:           'ChartFilterAlertMessages',
              chartType:           #LINE,
              dimensions:          [ 'AlertWIMessage' ],
              measures:            [ 'OrdersQuantity' ],
              dimensionAttributes: [{ dimension: 'AlertWIMessage', role: #CATEGORY }],
              measureAttributes:   [{ measure: 'OrdersQuantity', role: #AXIS_1 }]
            },
            { //visual filter
              qualifier:           'ChartFilterAlertMonth',
              chartType:           #BAR,
              dimensions:          [ 'AlertWIMonth' ],
              measures:            [ 'OrdersQuantity' ],
              dimensionAttributes: [{ dimension: 'AlertWIMonth', role: #CATEGORY }],
              measureAttributes:   [{ measure: 'OrdersQuantity', role: #AXIS_1 }]
            },
            { //visual filter
              qualifier:           'ChartFilterAlertQuarter',
              chartType:           #DONUT,
              dimensions:          [ 'AlertWIQuarter' ],
              measures:            [ 'OrdersQuantity' ],
              dimensionAttributes: [{ dimension: 'AlertWIQuarter', role: #CATEGORY }],
              measureAttributes:   [{ measure: 'OrdersQuantity', role: #AXIS_1 }]
            },
            { //main area chart  
              qualifier:           'ChartDefault',
              chartType:           #COLUMN,
              dimensions:          [ 'AlertWIMessage' ],
              measures:            [ 'OrdersQuantity' ],
              dimensionAttributes: [{ dimension: 'AlertWIMessage', role: #SERIES }],
              measureAttributes:   [{ measure: 'OrdersQuantity', role: #AXIS_1 }]
            }
           ]


annotate view zpoco_c_alp_alert_wi with
{
  
  @UI.facet: [
---------------------------------------------------------------------
//                       Header Facet Annotations                        
              {
                 id:              'HeaderOrderNumber',
                 purpose:          #HEADER,
                 type:             #DATAPOINT_REFERENCE,
                 targetQualifier: 'OrderNumber',
                 position: 10
              },
              {
                 id:              'HeaderAlertMessage',
                 purpose:          #HEADER,
                 type:             #DATAPOINT_REFERENCE,
                 targetQualifier: 'AlertWIMessage',
                 position: 20
              },
                            
---------------------------------------------------------------------            
//                       Object Page Tabs                                               
              {
                 id:                 'GeneralInformation',
                 type:               #COLLECTION,
                 label:              'General Information',
                 position:           10
              } ,
              {
                 id:                 'DateInformation',
                 type:               #COLLECTION,
                 label:              'Date Information',
                 position:           20
              },
 --------------------------------------------------------------------             
//                      Field Groups              
              
              {
                 id:                'BasicData',
                 purpose:           #STANDARD ,
                 parentId:          'GeneralInformation',
                 type:              #FIELDGROUP_REFERENCE,
                 label:             'Basic Data',
                 position:          10,
                 targetQualifier:   'BasicData'
              },
              {     
                 id:                'AdminData',     
                 purpose:           #STANDARD ,     
                 parentId:          'DateInformation',     
                 type:              #FIELDGROUP_REFERENCE,     
                 label:             'Date Information',     
                 position:          20,     
                 targetQualifier:   'DateInfo'     
              } 
             ]  
-----------------------------    
  
  @UI: { 
         lineItem:       [ { position: 10 } ],
         selectionField: [ { position: 10 } ]//,
//         fieldGroup:     [ { position: 10, qualifier: 'BasicData' } ]
       }
  UpdatedDate;               
  
  @UI: { 
         lineItem:   [ { position: 20 }],
         fieldGroup: [ { position: 20, qualifier: 'BasicData'}],
         dataPoint:  { qualifier: 'OrderNumber', title: 'Order Number' }
       }
  NvsId;               
  
  @UI: { 
         lineItem:   [ { position: 30 } ],
         fieldGroup: [ { position: 30, qualifier: 'BasicData' } ],
         dataPoint:  { qualifier: 'AlertWIMessage', title: 'Alert WI Message' }
       }
  AlertWIMessage;            

  @UI.lineItem:  [{ position: 80 }]
  @UI.dataPoint: { title: 'WIQ', 
                   qualifier: 'OrdersQuantity',
                   criticalityCalculation: { 
                                             improvementDirection:    #MINIMIZE,
                                             toleranceRangeHighValue:  12,
                                             deviationRangeHighValue:  15
                                           }
                 }
  OrdersQuantity;
-----------------------------  
  @UI: { 
         lineItem:   [ { position: 50 } ],
         fieldGroup: [ { position: 10, qualifier: 'DateInfo' } ]
       }
  AlertWIDay;                
  
  @UI: { 
         lineItem:       [ { position: 60 } ],
         selectionField: [ { position: 30 } ],
         fieldGroup:     [ { position: 20, qualifier: 'DateInfo' } ]
       }
  AlertWIMonth;              

  @UI: { 
         lineItem:       [ { position: 65 } ],
         selectionField: [ { position: 35 } ],
         fieldGroup:     [ { position: 25, qualifier: 'DateInfo'  } ]
       }
  AlertWIQuarter;            
  
  @UI: { 
         lineItem:       [ { position: 70 } ],
         selectionField: [ { position: 40 } ],
         fieldGroup:     [ { position: 30, qualifier: 'DateInfo'  } ]
       }
  AlertWIYear;               

}