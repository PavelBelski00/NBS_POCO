@Metadata.layer: #CORE
@UI.headerInfo: { typeName: 'Capacity',
                  typeNamePlural: 'Capacity' }
@UI.selectionPresentationVariant:[
    {  //chart and table
        qualifier: 'Default',
        presentationVariantQualifier: 'Default',
        selectionVariantQualifier: 'Default'
    },
    {   //kpi
        qualifier: 'KPIConsumedQuantityByYear',
        presentationVariantQualifier: 'pvKPIConsumedQuantityByYear',
        selectionVariantQualifier: 'svKPIConsumedQuantityByYear'
    },
    {   //kpi
        qualifier: 'KPIAvailableQuantityByYear',
        presentationVariantQualifier: 'pvKPIAvailableQuantityByYear',
        selectionVariantQualifier: 'svKPIAvailableQuantityByYear'
    }
]
@UI.presentationVariant:[
    { //main area chart
        qualifier: 'Default',
        sortOrder: [{ by: 'MfgStartDateYear'    , direction: #DESC },
                    { by: 'MfgStartDateQuarter' , direction: #ASC },
                    { by: 'MfgStartDateMonth'   , direction: #ASC },
                    { by: 'MfgStartDate'        , direction: #ASC }],
        visualizations: [{
            type: #AS_CHART,
            qualifier: 'ChartDefault'
        }]
    },
    {   //visual filter
        qualifier: 'pvFilterByTherapyType',
        text: 'Filter: by TherapyType',
        sortOrder: [{ by: 'TherapyTypeId', direction: #ASC }] ,
        visualizations: [{
            type: #AS_CHART,
            qualifier: 'ChartFilterByTherapyType'
        }]
    },
    {   //visual filter
        qualifier: 'pvFilterByQuarter',
        text: 'Filter: by Quarter',
        sortOrder: [{ by: 'MfgStartDateQuarter', direction: #ASC }] ,
        visualizations: [{
            type: #AS_CHART,
            qualifier: 'ChartFilterByQuarter'
        }]
    },
    {   //visual filter
        qualifier: 'pvFilterByCountry',
        text: 'Filter: by Country',
        sortOrder: [{ by: 'CountryId', direction: #ASC }] ,
        visualizations: [{
            type: #AS_CHART,
            qualifier: 'ChartFilterByCountry'
        }]
    },
    {   //kpi
        qualifier: 'pvKPIConsumedQuantityByYear',
        text: 'KPI Consumed Quantity By Year',
        visualizations: [{
            type: #AS_CHART,
            qualifier: 'cqKPIConsumedQuantityByYear'
        },{
            type: #AS_DATAPOINT,
            qualifier: 'ConsumedQuantity'
        }]
    },
    {   //kpi
        qualifier: 'pvKPIAvailableQuantityByYear',
        text: 'KPI Available Quantity By Year',
        visualizations: [{
            type: #AS_CHART,
            qualifier: 'cqKPIAvailableQuantityByYear'
        },{
            type: #AS_DATAPOINT,
            qualifier: 'AvailableQuantity'
        }]
    }

]
@UI.selectionVariant:[
    { //main area
        qualifier: 'Default',
        text: 'Default'
    },
    { //kpi
        qualifier: 'svKPIConsumedQuantityByYear',
        text: 'KPI Consumed Quantity By Year'
    },
    { //kpi
        qualifier: 'svKPIAvailableQuantityByYear',
        text: 'KPI Available Quantity By Year'
    }
]
@UI.chart: [
    {
        qualifier: 'ChartDefault',
        chartType: #COLUMN,
        dimensions:  [ 'MfgPlantId' ],
        measures:  [ 'Quantity' ],
        dimensionAttributes: [{
            dimension: 'MfgPlantId',
            role: #SERIES
        }],
        measureAttributes: [
        {   measure: 'Quantity',
            role: #AXIS_1
        }]
    },
    {   //visual filter
        qualifier: 'ChartFilterByTherapyType',
        chartType: #COLUMN,
        dimensions:  [ 'TherapyTypeId' ],
        measures:  [ 'Quantity' ],
        dimensionAttributes: [{
            dimension: 'TherapyTypeId',
            role: #CATEGORY
        }],
        measureAttributes: [
        {   measure: 'Quantity',
            role: #AXIS_1
        }]
    },
    {   //visual filter
        qualifier: 'ChartFilterByQuarter',
        chartType: #LINE,
        dimensions:  [ 'MfgStartDateQuarter' ],
        measures:  [ 'Quantity' ],
        dimensionAttributes: [{
            dimension: 'MfgStartDateQuarter',
            role: #CATEGORY
        }],
        measureAttributes: [
        {   measure: 'Quantity',
            role: #AXIS_1
        }]
    },
    {   //visual filter
        qualifier: 'ChartFilterByCountry',
        chartType: #DONUT,
        dimensions:  [ 'CountryId' ],
        measures:  [ 'Quantity' ],
        dimensionAttributes: [{
            dimension: 'CountryId',
            role: #CATEGORY
        }],
        measureAttributes: [
        {   measure: 'Quantity',
            role: #AXIS_1
        }]
    },
    { //kpi
        qualifier: 'cqKPIConsumedQuantityByYear',
        chartType: #LINE,
        dimensions:  [ 'MfgStartDateYear' ],
        measures:  [ 'ConsumedQuantity' ],
        dimensionAttributes: [{
            dimension: 'MfgStartDateYear',
            role: #CATEGORY
        }],
        measureAttributes: [{
            measure: 'ConsumedQuantity',
            role: #AXIS_1
        }]
    },
    { //kpi
        qualifier: 'cqKPIAvailableQuantityByYear',
        chartType: #LINE,
        dimensions:  [ 'MfgStartDateYear' ],
        measures:  [ 'AvailableQuantity' ],
        dimensionAttributes: [{
            dimension: 'MfgStartDateYear',
            role: #CATEGORY
        }],
        measureAttributes: [{
            measure: 'AvailableQuantity',
            role: #AXIS_1
        }]
    }
]
annotate view zpoco_c_alp_capacity with
{
  @UI.facet: [
          {     
                id:                 'HeaderInfo',
                purpose:            #HEADER,
                type:               #FIELDGROUP_REFERENCE,
                targetQualifier:    'HeaderInfo'
                
          },
          {
                id:                 'GeneralInformation',
                type:               #COLLECTION,
                label:              'General Information',
                position:           10
          },
          {
                id:                 'AdminDataCollection',
                type:               #COLLECTION,
                label:              'Admin Data',
                position:           20
          },
          {
                 id:                'BasicData',
                 purpose:           #STANDARD ,
                 parentId:          'GeneralInformation',
                 type:              #FIELDGROUP_REFERENCE,
                 label:             'Basic Data',
                 position:          10,
                 targetQualifier:   'BasicData'
          },
          {
                 id:                'AdminData',
                 purpose:           #STANDARD ,
                 parentId:          'AdminDataCollection',
                 type:              #FIELDGROUP_REFERENCE,
                 label:             'Admin Data',
                 position:          20,
                 targetQualifier:   'AdminData'
           } ]

  @UI.hidden: true
  CapacityUuid;

  @UI.selectionField: [{ position: 5 }]
  @UI.lineItem:       [{ position: 5, importance: #HIGH }]
  MfgStartDateYear;

  @UI.selectionField: [{ position: 6 }]
  @UI.lineItem:       [{ position: 6, importance: #HIGH }]
  MfgStartDateQuarter;

  @UI.selectionField: [{ position: 7 }]
  @UI.lineItem:       [{ position: 7, importance: #HIGH }]
  MfgStartDateMonth;

  @UI.selectionField: [{ position: 10 }]
  @UI.lineItem:       [{ position: 10, importance: #HIGH }]
  @UI.fieldGroup:     [{ position: 10, qualifier: 'HeaderInfo' }]
  MfgStartDate;
  
  @UI.lineItem:       [{ position: 20, importance: #HIGH }]
  @UI.fieldGroup:     [{ position: 10, qualifier: 'BasicData' }]
  NvsId;

  @UI.selectionField: [{ position: 30 }]
  @UI.lineItem:       [{ position: 30, importance: #HIGH }]
  @UI.fieldGroup:     [{ position: 30, qualifier: 'BasicData' }]
  MfgPlantId;

  @UI.selectionField: [{ position: 30 }]
  @UI.lineItem:       [{ position: 30, importance: #HIGH }]
  @UI.fieldGroup:     [{ position: 30, qualifier: 'BasicData' }]
  CountryId;

  @UI.selectionField: [{ position: 40 }]
  @UI.lineItem:       [{ position: 40, importance: #HIGH }]
  @UI.fieldGroup:     [{ position: 40, qualifier: 'BasicData' }]
  TherapyTypeId;


  @UI.lineItem:       [{ position: 50, importance: #HIGH }]
  @UI.fieldGroup:     [{ position: 50, qualifier: 'BasicData' }]
  ConsumedSlots;

  @UI.lineItem:       [{ position: 53, importance: #HIGH }]
  Quantity;

  @UI.dataPoint: {
       qualifier: 'ConsumedQuantity',
       title: 'Consumed Slots Quantity',
       criticalityCalculation: {
           improvementDirection: #MAXIMIZE,
           toleranceRangeLowValue:  100,
           toleranceRangeHighValue: 200
        }
  }
  ConsumedQuantity;

  @UI.dataPoint: {
       qualifier: 'AvailableQuantity',
       title: 'Available Slots Quantity',
       criticalityCalculation: {
           improvementDirection: #MINIMIZE,
           deviationRangeHighValue: 50,
           deviationRangeLowValue:  150
        }
  }
  AvailableQuantity;

  @UI.fieldGroup:     [{ position: 60, qualifier: 'BasicData' }]
  Priority;

  @UI.fieldGroup:     [{ position: 70, qualifier: 'BasicData' }]
  ProtocolSubjectId;

  @UI.fieldGroup:     [{ position: 80, qualifier: 'BasicData' }]
  CountryAllocation;

  @UI.fieldGroup:     [{ position: 10, qualifier: 'AdminData' }]
  CreatedBy;

  @UI.fieldGroup:     [{ position: 20, qualifier: 'AdminData' }]
  CreatedAt;

  @UI.fieldGroup:     [{ position: 30, qualifier: 'AdminData' }]
  LastChangedBy;

  @UI.fieldGroup:     [{ position: 40, qualifier: 'AdminData' }]
  LastChangedAt;

}